{
  parserClass="net.sjrx.intellij.plugins.systemdunitfiles.generated.UnitFileParser"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="UnitFile"
  psiImplClassSuffix="Impl"
  psiPackage="net.sjrx.intellij.plugins.systemdunitfiles.psi"
  psiImplPackage="net.sjrx.intellij.plugins.systemdunitfiles.psi.impl"

  elementTypeHolderClass="net.sjrx.intellij.plugins.systemdunitfiles.generated.UnitFileElementTypeHolder"
  elementTypeClass="net.sjrx.intellij.plugins.systemdunitfiles.psi.UnitFileElementType"
  tokenTypeClass="net.sjrx.intellij.plugins.systemdunitfiles.psi.UnitFileTokenType"

  extends("section_groups") = "net.sjrx.intellij.plugins.systemdunitfiles.psi.UnitFileSectionType"
  extends("property") = "net.sjrx.intellij.plugins.systemdunitfiles.psi.UnitFilePropertyType"
}

unitFile ::= (comment_|crlf_)* section_groups*

section_groups ::= section_ line_items_* {name="section header"}

private line_items_ ::= ((property crlf_?)|(comment_ crlf_?)|crlf_)

property ::= key_ separator_ value_? { pin=1 }

private value_ ::= (CONTINUING_VALUE| comment_ )* COMPLETED_VALUE
//private value_ ::= CONTINUING_VALUE CRLF COMMENT CRLF COMPLETED_VALUE

// Single rules for tokens so that I can alias them for better error handling shown above.
private comment_ ::= COMMENT {name="comment" }
private crlf_ ::= CRLF {name="new line"}
private section_ ::= SECTION

private key_ ::= KEY
private separator_ ::= SEPARATOR { name="key-value separator (=)"}

